# SPDX-FileCopyrightText: 2025 Ryan Lahfa <ryan.lahfa.ext@numerique.gouv.fr>
#
# SPDX-License-Identifier: MIT

{
  pkgs,
  config,
  lib,
  ...
}:
let
  inherit (lib) mkIf mkOption types;
  cfg = config.securix.graphical-interface;
  plasmaSetupScript =
    # It takes the homedir as parameter in order to be injected by systemd
    pkgs.writeShellScript "plasma-setup" (
      ''
        homedir=$1
      ''
      + (lib.optionalString (!cfg.kde.kwallet.enable) ''
        mkdir -p "$homedir/.config/"
        printf '# Generated by the systemd service configure-plasma\n[Wallet]\nEnabled=false' > "$homedir/.config/kwalletrc"
      '')
    );
  plasmaDoSetup = !cfg.kde.kwallet.enable;
in
{
  options.securix.graphical-interface.kde = {
    lookAndFeelPackages = mkOption {
      type = types.listOf types.package;
      default = [ ];
      description = "List of extra Look & Feel packages";
    };

    defaultLookAndFeel = mkOption {
      type = types.str;
      default = "org.kde.breeze";
      description = "Default look & feel package";
      example = "fr.dinum.bureautix";
    };
    kwallet.enable = mkOption {
      type = types.bool;
      default = true;
      description = "Whether to enable kwallet";
    };
  };
  config = mkIf (cfg.variant == "kde") {
    services.xserver.enable = true;
    services.displayManager.sddm.enable = true;
    services.desktopManager.plasma6.enable = true;

    systemd.user.services.plasma-setup = lib.mkIf plasmaDoSetup {
      description = "Plasma Setup";
      wantedBy = [ "graphical-session-pre.target" ];
      serviceConfig = {
        Type = "oneshot";
        ExecStart = "${plasmaSetupScript} %h";
      };
    };

    environment.systemPackages = cfg.kde.lookAndFeelPackages;
    environment.etc."xdg/kdeglobals".text = ''
      [KDE]
      LookAndFeelPackage=${cfg.kde.defaultLookAndFeel}
    '';
  };
}
